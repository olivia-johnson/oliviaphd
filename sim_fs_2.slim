initialize() {
	initializeMutationRate(1e-7);
	initializeMutationType("m1", 0.5, "f", 0.0); // neutral
	initializeMutationType("m2", 0.5, "g", -0.01, 0.1); // deleterious
	initializeMutationType("m3", 0.5, "f", 0.9); // beneficial
	m1.color="gray40";
	m2.color="red";
	m3.color = "green";
	initializeGenomicElementType("g1", m1, 1.0);
	initializeGenomicElementType("g2", m1, 1.0);
	initializeGenomicElementType("g3", m1, 1.0);
	initializeGenomicElementType("g4", m2, 1.0);
	
	lines = readFile("~/Downloads/GStest.txt");
	for (line in lines) {
		components = strsplit(line, "\t");
		start = asInteger(components[0]);
		end = asInteger(components[1]);
		type = components[2];
		if (type == 'intergenic') {
			initializeGenomicElement(g1, start, end);
		}
		if (type == 'exon') {
			initializeGenomicElement(g2, start, end);
		}
		if (type == 'intron') {
			initializeGenomicElement(g3, start, end);
		}
		if (type == 'conserved_nc') {
			initializeGenomicElement(g4, start, end);
		}
	}
	
	rec = readFile("~/Downloads/RRtest.txt");
	rates = NULL;
	ends = NULL;
	
	for (x in rec)
	{
		rr = strsplit(x, "\t");
		ends = c(ends, asInteger(rr[0]));
		rates = c(rates, asFloat(rr[1]));
	}
	
	ends = c(ends[1:(size(ends)-1)] - 2, 20000);
	rates = rates * 1e-8;
	initializeRecombinationRate(rates, ends);

}

1 { sim.addSubpop("p1", 1000); }


1000 late(){
	s_ind = sample(p1.genomes, 100);
	s_ind.addNewDrawnMutation(m3, 8000);
}

1000:10000 late(){
	modValue = integerMod(sim.generation, 10);
	if (modValue == 0) 
	{
		sim.mutationsOfType(m3).setSelectionCoeff(0.1);
	}
	if (modValue == 5) 
	{
		sim.mutationsOfType(m3).setSelectionCoeff(-0.01);
	}
}


10000 { sim.simulationFinished(); }