
initialize() {
	if (!exists("tmpdir"))
	{defineConstant("tmpdir", "~/phd_data/seglift_l10/group_4");} // call in tmpdir
	
	if (!exists("results_dir"))
	{defineConstant("results_dir", "~/phd_data/Results/background/group_4");} // call in results dir
	
	if (!exists("GenomeSize"))
	{defineConstant("GenomeSize", 10500000); } // genome size
	
	if (!exists("nChrom"))
	{defineConstant("nChrom", 21); } // number of chromosomes
	
	if (!exists("L"))
	{defineConstant("L", 10); }// no. selected seasonal loci
	
	if (!exists("y"))
	{defineConstant("y", 1); }// exponent of fitness function
	
	if (!exists("n_s"))
	{defineConstant("n_s", 1e4); }// summer population size
	
	if (!exists("n_w"))
	{defineConstant("n_w", 1e4); }// winter population size
	
	if (!exists("d"))
	{defineConstant("d", 0.65); } // dominance
	
	if (!exists("g_s"))
	{defineConstant("g_s", 15); } // generations per summer
	
	if (!exists("g_w"))
	{defineConstant("g_w", 15); } // generations per winter 
	
	if (!exists("g"))
	{defineConstant("g", (g_s + g_w)); } // generations per seasonal cycle     
	
	if (!exists("mut"))
	{defineConstant("mut", 0.0);} // mutation rate
	
	if (!exists("rr"))
	{defineConstant("rr", 1e-6);} // recombination rate
	
	if (!exists("rGen"))
	{defineConstant("rGen", 60);} // remember individuals gen call
	
	if (!exists("group"))
	{defineConstant("group", 4);} // group of parameters
	
	if (!exists("sim_run"))
	{defineConstant("sim_run", 0);} // sim run
	
	if (!exists("fit"))
	{defineConstant("fit",1);} //if fit = 0 fitness function switched off
	initializeSLiMOptions(keepPedigrees=T)	;
	initializeMutationRate(mut);
	
	lines = readFile(paste(asString(tmpdir)+"/rec_map_group_"+asString(group)+".txt"));
	rates = NULL;
	ends = NULL;
	
	for (line in lines){
		components = strsplit(line, "\t");
		ends = c(ends, asInteger(components[0]));
		rates = c(rates, asFloat(components[1]));
	}
	initializeRecombinationRate(rates,ends);
	
	initializeMutationType("m1", 0.0, "f", 0.0);  // seasonal mutations
	initializeGenomicElementType("g1", m1, 1.0);
	initializeGenomicElement(g1, 0, GenomeSize-1);
	m1.color = "pink";
	m1.convertToSubstitution = F;
	
	initializeTreeSeq();
}

/// "/tmp/lls_" + asString(tmpdir)+"/al_freq_group_"  HPC TMPDIR PATH
1 late() {
	cat(sim_run + "\n");
	line1 = paste(c("SimID:" + sim_run +"\n" + "Fitness_on=" + fit+ ", n_s=" + n_s + ", n_w=" + n_w+ ", GenomeSize=" + GenomeSize + ", L=" + L +", d=" + d + ", y=" +y + ", g_s=" + g_s + ", g_w="+g_w));
	
	if (fileExists(paste(asString(results_dir)+"/burnin_seglift_group_"+ asString(group) + "_"+ asString(sim_run) + ".trees"))==T){
		sim.readFromPopulationFile(paste(asString(results_dir)+ "/burnin_seglift_group_"+ asString(group) + "_"+ asString(sim_run) + ".trees"));} else{
		sim.readFromPopulationFile(paste(asString(tmpdir)+ "/burnin_seglift_group_"+ asString(group) + "_"+ asString(sim_run) + ".trees"));}
	
	
	//	sim.addSubpop("p0", n_s);
	
	
	if (L > 0){
		win = (GenomeSize-(L-9))/nChrom;
		win_mid = win/2;
		
		for (i in 1:10){
			pos = (i*(2*win))-win_mid; // 1 selected site every second chromsome
			//pos = (i*win)-win_mid; // 1 selected site per chromosome
			
			target = sample(p0.genomes, asInteger(n_s));
			target.addNewDrawnMutation(m1, asInteger(pos));
		}
		if (L >10){
			for (i in 1:(L-10)){
				pos = (GenomeSize-(L-10)-1+i);
				target = sample(p0.genomes, asInteger(n_s));
				target.addNewDrawnMutation(m1, asInteger(pos));
			}
		}
		if (sim.mutationFrequencies(p0)[1] != 0.5){
			sim.simulationFinished();
			catn("ERROR: SELECTED MUTATIONS NOT AT 0.5 STARTING FREQUENCY");
		}
		
		
		line3 = "FREQUENCIES \nGen,mut_pos,mut_freq";
		writeFile(paste(asString(tmpdir)+ "/al_freq_group_"+ asString(group) + "_"+ asString(sim_run)+".txt"), line3, append = F);
		writeFile(paste(asString(tmpdir)+"/pedigree_group_"+ asString(group) + "_"+ asString(sim_run)+".txt"), paste("Gen","Ind","P1","P2", "G1", "G2", "G3", "G4" , sep=","), append = F);
		for (i in 1:size(sim.mutations)-1){
			tab_line = paste(c(sim.generation,sim.mutations[i].position,sim.mutationFrequencies(p0)[i]), sep = ",");
			writeFile(paste(asString(tmpdir)+ "/al_freq_group_"+ asString(group) + "_"+ asString(sim_run)+".txt"), tab_line, append = T);
		}
	}
}

s1 fitness(NULL) { //summer
	if (fit == 0){
		return 1.0;
	} else{
		genomes=individual.genomes.mutationFrequenciesInGenomes();
		ns = sum(genomes==1); //homozygosity summer
		nhet = sum(genomes==0.5);  // heterozygosity
		nw = sum(genomes==0);//homozygosity winter
		if (modValueSeason >= 0 & modValueSeason < g_s) { //summer
			zs = ns + (d * nhet);
			return exp(zs^y); }
		else { // winter
			zw = nw + (d * nhet);
			return exp(zw^y);}
	}
}

1:120000 early(){
	defineGlobal("gen", sim.generation);
	defineGlobal("modValueSeason",integerMod(gen, g));
	defineGlobal("modValuerGen", integerMod(gen, rGen));
	
	if (modValuerGen ==0){
		catn(gen);}

}

1:120000 late(){
	defineGlobal("fixed", sum(sim.mutationFrequencies(p0)==1));
	if (modValueSeason >= 0 & modValueSeason < g_s) { //summer
		if (modValueSeason == 0){
			p0.setSubpopulationSize(n_s);}
		if (exists("slimgui")) {cat("Summer\n");}
	}
	if (modValueSeason >= g_s) { //winter
		if (modValueSeason == g_s){
			p0.setSubpopulationSize(n_w);}
		if (exists("slimgui")) {cat("Winter\n");}
	}
	
	if (modValuerGen >=0 & modValuerGen <=((g_s + g_w)-1)) {
		if (exists("slimgui")) {catn("Gen:" + gen + " sampling");}
		if (L > 0){
			for (i in 1:size(sim.mutations)-1){
				tab_line = paste(c(gen,sim.mutations[i].position,sim.mutationFrequencies(p0)[i]), sep = ",");
				writeFile(paste(asString(tmpdir)+"/al_freq_group_"+ asString(group) + "_"+ asString(sim_run)+".txt"), tab_line, append = T);
			
			}
		}
		rInd= sample(p0.individuals, 100);
		sim.treeSeqRememberIndividuals(rInd);
	}
}
2:120000 early(){
	
	
	if (exists("slimgui")) {cat(mean(p0.cachedFitness(NULL)) + "\n");}
	
	if (modValuerGen >= rGen-2 | modValuerGen >= 0 & modValuerGen <=(g_s + g_w)) {
		pedID=p0.individuals.pedigreeID;
		
		for (ind in 1:p0.individualCount-1){
			indv = p0.individuals[ind].pedigreeID;
			parents = p0.individuals[ind].pedigreeParentIDs;
			grandparents = p0.individuals[ind].pedigreeGrandparentIDs;
			writeFile(paste(asString(tmpdir)+"/pedigree_group_"+ asString(group) + "_"+ asString(sim_run)+".txt"), paste(gen, indv, parents, grandparents, sep=","), append = T);
		}
	}
}



120000 late() {
	gen = sim.generation;
	if (L > 0){
		for (i in 1:size(sim.mutations)-1){
			tab_line = paste(c(gen,sim.mutations[i].position,sim.mutationFrequencies(p0)[i]), sep = ",");
			writeFile(paste(asString(tmpdir)+"/al_freq_group_"+ asString(group) + "_"+ asString(sim_run)+".txt"), tab_line, append = T);
		}
	}
	e_mc = sim.countOfMutationsOfType(m1);
	loss = L - e_mc;
	
	
	mem = usage(peak=T);
	line4 = paste("\nwinter_fixed:" + loss + "\nsummer_fixed:" + fixed +"\nMemory usage: "+ mem);
	catn(line4);
	
	sim.treeSeqOutput(paste(asString(tmpdir)+ "/treeseq_group_"+ asString(group) + "_" + asString(sim_run) + ".trees"));
}

